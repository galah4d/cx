package gui

//-----------------------------------------------------------------------------
// WINDOW
//-----------------------------------------------------------------------------
var g_windows[] window_s

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
type window_id struct {
	base ControlId
	window i32
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
type window_s struct {
	id window_id
	header ControlId
	caption LabelId
	container ControlId
	strip ControlId
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_id_(base ControlId, window i32) (out window_id) {
	out.base = base
	out.window = window
	panicIfNot(is_valid_window(out), "invalid id")
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func is_valid_window(id window_id) (out bool) {
	out = id.window >= 0 && id.window < len(g_windows) && IsValidControl(id.base)
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_set_caption(id window_id, text str) () {
	panicIfNot(is_valid_window(id), "invalid id")
	LabelSetText(g_windows[id.window].caption, text)
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_set_containee(id window_id, containee ControlId) {
	panicIfNot(is_valid_window(id), "invalid id")
	panicIfNot(IsValidControl(containee), "invalid containee")

	var container ControlId = g_windows[id.window].container
	panicIfNot(IsValidControl(container), "invalid container")

	ControlAddChild(container, containee)
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_create(name str) (out window_id) {
	out.base = controlCreate(name, GUI_WINDOW, out.window)
	out.window = len(g_windows)

	var window window_s
	window.id = out

	var padding mat.v4 = mat.v4_(5.0, 5.0, 5.0, 5.0)
	var padding1 mat.v4 = mat.v4_(3.0, 3.0, 3.0, 3.0)
	{
		var header ControlId = ControlCreate("header") // ##pending := 
		ControlAddChild(out.base, header)
		ControlSetSkin(header, g_headerSkin)
		ControlSetSize(header, mat.v2_(0.0, 0.1))
		ControlSetDock(header, DOCK_TOP)
		ControlSetAutosize(header, AUTOSIZE_Y)
		window.header = header

		{
			var caption LabelId = LabelCreate("label")
			ControlAddChild(header, caption.base)
			ControlSetDock(caption.base, DOCK_FILL)
			ControlSetAutosize(caption.base, AUTOSIZE_Y)
			LabelSetAlign(caption, ALIGN_CENTER)
			LabelSetFont(caption, g_boldFont) // ##1 skin
			LabelSetColor(caption, g_white)
			LabelSetSize(caption, g_fontSizeText)
			window.caption = caption
		}
	}

	{
		var container ControlId = ControlCreate("container")
		ControlAddChild(out.base, container)
		//ControlSetSkin(container, g_redSkin)
		ControlSetDock(container, DOCK_TOP)
		ControlSetAutosize(container, AUTOSIZE)
		ControlSetPadding(container, padding1)
		window.container = container
	}

	{
		var strip ControlId = ControlCreate("strip")
		ControlAddChild(out.base, strip)
		//ControlSetSkin(strip, g_blueSkin)
		ControlSetBounds(strip, mat.v4_(0.0, 0.0, 0.0, 0.12))
		ControlSetDock(strip, DOCK_TOP)
		ControlSetAutosize(strip, AUTOSIZE_Y)
		ControlSetPadding(strip, padding)
		window.strip = strip
	}

	g_windows = append(g_windows, window)
	panicIfNot(is_valid_window(out), "invalid id")
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_add_button(id window_id, name str, caption str, packageName str, functionName str) {
	panicIfNot(is_valid_window(id), "invalid id")

   var button LabelId = LabelCreate(name)
   ControlSetSkin(button.base, g_buttonSkin)
   ControlSetBounds(button.base, mat.v4_(0.0, 0.0, 50.0, 0.0))
   ControlSetDock(button.base, DOCK_RIGHT)
   ControlSetAutosize(button.base, AUTOSIZE_Y)
   ControlSetPadding(button.base, mat.v4_(0.0, 0.0, 0.0, 0.0))
   ControlSetFocusable(button.base, 1)
   LabelSetAlign(button, ALIGN_CENTER)
   LabelSetColor(button, g_white)
   LabelSetFont(button, g_boldFont)
   LabelSetText(button, caption)
   LabelSetSize(button, g_fontSizeSmall)
   LabelSetOnState(button, packageName, functionName)

   var strip ControlId = g_windows[id.window].strip
   ControlAddChild(strip, button.base)
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_process_event(id window_id, event *app.Event) (out i32) {
	panicIfNot(is_valid_window(id), "invalid id")
	out = ControlProcessEvent(id.base, event)
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_resize(id window_id) () {
	panicIfNot(is_valid_window(id), "invalid id")
	ControlResize(id.base)
}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
func window_render(id window_id, clip mat.v4, stack i32) () {
	panicIfNot(is_valid_window(id), "invalid id")
	ControlRender(id.base, clip, stack)
}


