package main

import "app"
import "fps"
import "mat"
import "gfx"
import "gl" // ##1 remove gl dependency

var exit bool // ISSUE : panic when Application is global

func KeyboardCallback(window str, key i32, scancode i32, action i32, mods i32) () {
	printf("KeyboardCallback : window %s, key %d, scancode %d, action %d, mods %d\n", window, key, scancode, action, mods)
	if key == 256 && action == 1 {
		exit = true
	}
}

func MouseButtonCallback(window str, key i32, action i32, mods i32) () {
	printf("MouseButtonCallback : window %s, key %d, action %d, mods %d\n", window, key, action, mods)
}

func MousePositionCallback(window str, x f64, y f64) () {
	printf("MousePositionCallback : window %s, x %f, y %f\n", window, x, y)
}

func main()() {
	var o app.Options
	o.width = 800
	o.height = 600
	o.fps = 60
	o.Parse()

	var f fps.Framerate
	f.Init(i32.f64(o.fps))

	var prfSwap fps.ProfileId = fps.CreateProfile("swap")
	var prfFrame fps.ProfileId = fps.CreateProfile("frame")
	var prfUpdate fps.ProfileId = fps.CreateProfile("update")
	var prfRender fps.ProfileId = fps.CreateProfile("render")

	var a app.Application
	a.Init(o, "Window Tutorial", "mainWindow", 2, 1)
	a.SetKeyboardCallback("main", "KeyboardCallback")
	a.SetMouseButtonCallback("main", "MouseButtonCallback")
	a.SetMousePositionCallback("main", "MousePositionCallback")

	gfx.init(o.width, o.height)

	var width f32 = i32.f32(o.width)
	var height f32 = i32.f32(o.height)

	var texture gfx.texture_s = gfx.create_texture(sprintf("%s/textures/Skycoin-Cloud-BW-Vertical-on_black@2x.png", o.dataDir),
			gl.LINEAR, gl.LINEAR, gl.CLAMP_TO_EDGE, gl.CLAMP_TO_EDGE, gl.CLAMP_TO_EDGE, 0, 0)

	for app.Running(&a) { // ISSUE : can't use member function
		f.BeginUpdate(2.0D)
		fps.Start(prfSwap)
		{
			a.BeginFrame()
			fps.Start(prfFrame)
			{
				// Update ...
				fps.Start(prfUpdate)
				if exit {
					a.Exit()
				}
				var mesh gfx.mesh_id = gfx.lock_mesh(gl.TRIANGLES, gfx.g_vertexColorAttributes, 6)

				var w f32 = i32.f32(texture.width) / 10.0 // ISSUE : can't declare using :=, compilation errors follows when f32 arguments are expected
				var h f32 = i32.f32(texture.height) / 10.0
				var x f32 =  (width - w) / 2.0
				var y f32 = (height - h) / 2.0

				gfx.begin_mesh(mesh)
				gfx.append_quad(mesh,
						mat.v4_(x, y, w, h),
						mat.v4_(0.0, 0.0, 1.0, 1.0),
						mat.v4_(1.0, 1.0, 1.0, 1.0),
						mat.v4_(0.0, 0.0, width, height),
						0.0)

				gfx.end_mesh(mesh)
 				fps.Stop(prfUpdate)

				// Render ...
				fps.Start(prfRender)
				gfx.effect_use(gfx.g_fxTexture)
				gfx.effect_assign_texture(gfx.g_fxTexture, 0, texture.name)
				gfx.render_mesh(mesh)
				gfx.unlock_mesh(mesh)
				fps.Stop(prfRender)
			}
			fps.Stop(prfFrame)
			a.EndFrame()
		}
		fps.Stop(prfSwap)
		f.EndUpdate()
	}

	gfx.free()
}
